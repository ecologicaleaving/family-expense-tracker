// Root build.gradle for Flutter project

buildscript {
    ext.kotlin_version = '2.1.0'
    repositories {
        google()
        mavenCentral()
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }

    // Define flutter SDK version properties for all projects
    // This allows plugin build.gradle files to access them during evaluation
    ext {
        set('flutter.compileSdkVersion', 34)
        set('flutter.minSdkVersion', 24)
        set('flutter.targetSdkVersion', 34)
        set('flutter.ndkVersion', "26.1.10909125")
        set('flutter.versionCode', 1)
        set('flutter.versionName', "1.0.0")
    }
}

rootProject.buildDir = "../build"

subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"

    afterEvaluate { project ->
        if (project.hasProperty("android")) {
            android {
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_17
                    targetCompatibility JavaVersion.VERSION_17
                }
            }
        }

        // Force all Kotlin subprojects to use JVM 17
        tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
            kotlinOptions {
                jvmTarget = "17"
            }
        }
    }
}

subprojects {
    project.evaluationDependsOn(":app")
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
